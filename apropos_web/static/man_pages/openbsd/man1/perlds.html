<head>
<meta charset="utf-8"/>
<style>
table.head, table.foot { width: 100%; }
td.head-rtitle, td.foot-os { text-align: right; }
td.head-vol { text-align: center; }
table.foot td { width: 50%; }
table.head td { width: 33%; }
div.spacer { margin: 1em 0; }
</style>
<link rel="stylesheet" href="/static/man_pages/posix/style.css" type="text/css" media="all"/>
<title>
PERLDSC(1)</title>
</head>
<body>
<div class="mandoc">
<table class="head">
<tbody>
<tr>
<td class="head-ltitle">
PERLDSC(1)</td>
<td class="head-vol">
Perl Programmers Reference Guide</td>
<td class="head-rtitle">
PERLDSC(1)</td>
</tr>
</tbody>
</table>
<div class="section">
<h1>NAME</h1> perldsc - Perl Data Structures Cookbook</div>
<div class="section">
<h1>DESCRIPTION</h1> Perl lets us have complex data structures.  You can write something like this and all of a sudden, you'd have an array with three dimensions!<div class="spacer">
</div>
<br/>
    for $x (1 .. 10) {<br/>
        for $y (1 .. 10) {<br/>
            for $z (1 .. 10) {<br/>
                $AoA[$x][$y][$z] =<br/>
                    $x ** $y + $z;<br/>
            }<br/>
        }<br/>
    }<br/>
<div class="spacer">
</div>
Alas, however simple this may appear, underneath it's a much more elaborate construct than meets the eye!<div class="spacer">
</div>
How do you print it out?  Why can't you say just &quot;print @AoA&quot;?  How do you sort it?  How can you pass it to a function or get one of these back from a function?  Is it an object?  Can you save it to disk to read back later?  How do you access whole rows or columns of that matrix?  Do all the values have to be numeric?<div class="spacer">
</div>
As you see, it's quite easy to become confused.  While some small portion of the blame for this can be attributed to the reference-based implementation, it's really more due to a lack of existing documentation with examples designed for the beginner.<div class="spacer">
</div>
This document is meant to be a detailed but understandable treatment of the many different sorts of data structures you might want to develop.  It should also serve as a cookbook of examples.  That way, when you need to create one of these complex data structures, you can just pinch, pilfer, or purloin a drop-in example from here.<div class="spacer">
</div>
Let's look at each of these possible constructs in detail.  There are separate sections on each of the following:<dl>
<dt>
&#8226;</dt>
<dd>
arrays of arrays</dd>
</dl>
<dl>
<dt>
&#8226;</dt>
<dd>
hashes of arrays</dd>
</dl>
<dl>
<dt>
&#8226;</dt>
<dd>
arrays of hashes</dd>
</dl>
<dl>
<dt>
&#8226;</dt>
<dd>
hashes of hashes</dd>
</dl>
<dl>
<dt>
&#8226;</dt>
<dd>
more elaborate constructs</dd>
</dl>
<div class="spacer">
</div>
But for now, let's look at general issues common to all these types of data structures.</div>
<div class="section">
<h1>REFERENCES</h1> The most important thing to understand about all data structures in Perl--including multidimensional arrays--is that even though they might appear otherwise, Perl @ARRAYs and %HASHes are all internally one-dimensional.  They can hold only scalar values (meaning a string, number, or a reference).  They cannot directly contain other arrays or hashes, but instead contain  <i>references</i> to other arrays or hashes.<div class="spacer">
</div>
You can't use a reference to an array or hash in quite the same way that you would a real array or hash.  For C or C++ programmers unused to distinguishing between arrays and pointers to the same, this can be confusing.  If so, just think of it as the difference between a structure and a pointer to a structure.<div class="spacer">
</div>
You can (and should) read more about references in perlref. Briefly, references are rather like pointers that know what they point to.  (Objects are also a kind of reference, but we won't be needing them right away--if ever.)  This means that when you have something which looks to you like an access to a two-or-more-dimensional array and/or hash, what's really going on is that the base type is merely a one-dimensional entity that contains references to the next level.  It's just that you can  <i>use</i> it as though it were a two-dimensional one.  This is actually the way almost all C multidimensional arrays work as well.<div class="spacer">
</div>
<br/>
    $array[7][12]                       # array of arrays<br/>
    $array[7]{string}                   # array of hashes<br/>
    $hash{string}[7]                    # hash of arrays<br/>
    $hash{string}{'another string'}     # hash of hashes<br/>
<div class="spacer">
</div>
Now, because the top level contains only references, if you try to print out your array in with a simple  <i>print()</i> function, you'll get something that doesn't look very nice, like this:<div class="spacer">
</div>
<br/>
    @AoA = ( [2, 3], [4, 5, 7], [0] );<br/>
    print $AoA[1][2];<br/>
  7<br/>
    print @AoA;<br/>
  ARRAY(0x83c38)ARRAY(0x8b194)ARRAY(0x8b1d0)<br/>
<div class="spacer">
</div>
That's because Perl doesn't (ever) implicitly dereference your variables. If you want to get at the thing a reference is referring to, then you have to do this yourself using either prefix typing indicators, like &quot;${$blah}&quot;, &quot;@{$blah}&quot;, &quot;@{$blah[$i]}&quot;, or else postfix pointer arrows, like &quot;$a-&gt;[3]&quot;, &quot;$h-&gt;{fred}&quot;, or even &quot;$ob-&gt;method()-&gt;[3]&quot;.</div>
<div class="section">
<h1>COMMON MISTAKES</h1> The two most common mistakes made in constructing something like an array of arrays is either accidentally counting the number of elements or else taking a reference to the same memory location repeatedly.  Here's the case where you just get the count instead of a nested array:<div class="spacer">
</div>
<br/>
    for $i (1..10) {<br/>
        @array = somefunc($i);<br/>
        $AoA[$i] = @array;      # WRONG!<br/>
    }<br/>
<div class="spacer">
</div>
That's just the simple case of assigning an array to a scalar and getting its element count.  If that's what you really and truly want, then you might do well to consider being a tad more explicit about it, like this:<div class="spacer">
</div>
<br/>
    for $i (1..10) {<br/>
        @array = somefunc($i);<br/>
        $counts[$i] = scalar @array;<br/>
    }<br/>
<div class="spacer">
</div>
Here's the case of taking a reference to the same memory location again and again:<div class="spacer">
</div>
<br/>
    for $i (1..10) {<br/>
        @array = somefunc($i);<br/>
        $AoA[$i] = \@array;     # WRONG!<br/>
    }<br/>
<div class="spacer">
</div>
So, what's the big problem with that?  It looks right, doesn't it? After all, I just told you that you need an array of references, so by golly, you've made me one!<div class="spacer">
</div>
Unfortunately, while this is true, it's still broken.  All the references in @AoA refer to the  <i>very same place</i>, and they will therefore all hold whatever was last in @array!  It's similar to the problem demonstrated in the following C program:<div class="spacer">
</div>
<br/>
    #include &lt;pwd.h&gt;<br/>
    main() {<br/>
        struct passwd *getpwnam(), *rp, *dp;<br/>
        rp = getpwnam(&quot;root&quot;);<br/>
        dp = getpwnam(&quot;daemon&quot;);<br/>
<br/>
        printf(&quot;daemon name is %s\nroot name is %s\n&quot;,<br/>
                dp-&gt;pw_name, rp-&gt;pw_name);<br/>
    }<br/>
<div class="spacer">
</div>
Which will print<div class="spacer">
</div>
<br/>
    daemon name is daemon<br/>
    root name is daemon<br/>
<div class="spacer">
</div>
The problem is that both &quot;rp&quot; and &quot;dp&quot; are pointers to the same location in memory!  In C, you'd have to remember to  <i>malloc()</i> yourself some new memory.  In Perl, you'll want to use the array constructor &quot;[]&quot; or the hash constructor &quot;{}&quot; instead.   Here's the right way to do the preceding broken code fragments:<div class="spacer">
</div>
<br/>
    for $i (1..10) {<br/>
        @array = somefunc($i);<br/>
        $AoA[$i] = [ @array ];<br/>
    }<br/>
<div class="spacer">
</div>
The square brackets make a reference to a new array with a <i>copy</i> of what's in @array at the time of the assignment.  This is what you want.<div class="spacer">
</div>
Note that this will produce something similar, but it's much harder to read:<div class="spacer">
</div>
<br/>
    for $i (1..10) {<br/>
        @array = 0 .. $i;<br/>
        @{$AoA[$i]} = @array;<br/>
    }<br/>
<div class="spacer">
</div>
Is it the same?  Well, maybe so--and maybe not.  The subtle difference is that when you assign something in square brackets, you know for sure it's always a brand new reference with a new  <i>copy</i> of the data. Something else could be going on in this new case with the &quot;@{$AoA[$i]}&quot; dereference on the left-hand-side of the assignment.  It all depends on whether $AoA[$i] had been undefined to start with, or whether it already contained a reference.  If you had already populated @AoA with references, as in<div class="spacer">
</div>
<br/>
    $AoA[3] = \@another_array;<br/>
<div class="spacer">
</div>
Then the assignment with the indirection on the left-hand-side would use the existing reference that was already there:<div class="spacer">
</div>
<br/>
    @{$AoA[3]} = @array;<br/>
<div class="spacer">
</div>
Of course, this <i>would</i> have the &quot;interesting&quot; effect of clobbering @another_array.  (Have you ever noticed how when a programmer says something is &quot;interesting&quot;, that rather than meaning &quot;intriguing&quot;, they're disturbingly more apt to mean that it's &quot;annoying&quot;, &quot;difficult&quot;, or both?  :-)<div class="spacer">
</div>
So just remember always to use the array or hash constructors with &quot;[]&quot; or &quot;{}&quot;, and you'll be fine, although it's not always optimally efficient.<div class="spacer">
</div>
Surprisingly, the following dangerous-looking construct will actually work out fine:<div class="spacer">
</div>
<br/>
    for $i (1..10) {<br/>
        my @array = somefunc($i);<br/>
        $AoA[$i] = \@array;<br/>
    }<br/>
<div class="spacer">
</div>
That's because <i>my()</i> is more of a run-time statement than it is a compile-time declaration  <i>per se</i>.  This means that the <i>my()</i> variable is remade afresh each time through the loop.  So even though it  <i>looks</i> as though you stored the same variable reference each time, you actually did not!  This is a subtle distinction that can produce more efficient code at the risk of misleading all but the most experienced of programmers.  So I usually advise against teaching it to beginners.  In fact, except for passing arguments to functions, I seldom like to see the gimme-a-reference operator (backslash) used much at all in code.  Instead, I advise beginners that they (and most of the rest of us) should try to use the much more easily understood constructors &quot;[]&quot; and &quot;{}&quot; instead of relying upon lexical (or dynamic) scoping and hidden reference-counting to do the right thing behind the scenes.<div class="spacer">
</div>
In summary:<div class="spacer">
</div>
<br/>
    $AoA[$i] = [ @array ];      # usually best<br/>
    $AoA[$i] = \@array;         # perilous; just how my() was that array?<br/>
    @{ $AoA[$i] } = @array;     # way too tricky for most programmers<br/>
</div>
<div class="section">
<h1>CAVEAT ON PRECEDENCE</h1> Speaking of things like &quot;@{$AoA[$i]}&quot;, the following are actually the same thing:<div class="spacer">
</div>
<br/>
    $aref-&gt;[2][2]       # clear<br/>
    $$aref[2][2]        # confusing<br/>
<div class="spacer">
</div>
That's because Perl's precedence rules on its five prefix dereferencers (which look like someone swearing: &quot;$ @ * % &amp;&quot;) make them bind more tightly than the postfix subscripting brackets or braces!  This will no doubt come as a great shock to the C or C++ programmer, who is quite accustomed to using *a[i] to mean what's pointed to by the  <i>i'th</i> element of &quot;a&quot;.  That is, they first take the subscript, and only then dereference the thing at that subscript.  That's fine in C, but this isn't C.<div class="spacer">
</div>
The seemingly equivalent construct in Perl, $$aref[$i] first does the deref of $aref, making it take $aref as a reference to an array, and then dereference that, and finally tell you the  <i>i'th</i> value of the array pointed to by $AoA. If you wanted the C notion, you'd have to write &quot;${$AoA[$i]}&quot; to force the $AoA[$i] to get evaluated first before the leading &quot;$&quot; dereferencer.</div>
<div class="section">
<h1>WHY YOU SHOULD ALWAYS &quot;use strict&quot;</h1> If this is starting to sound scarier than it's worth, relax.  Perl has some features to help you avoid its most common pitfalls.  The best way to avoid getting confused is to start every program like this:<div class="spacer">
</div>
<br/>
    #!/usr/bin/perl -w<br/>
    use strict;<br/>
<div class="spacer">
</div>
This way, you'll be forced to declare all your variables with <i>my()</i> and also disallow accidental &quot;symbolic dereferencing&quot;.  Therefore if you'd done this:<div class="spacer">
</div>
<br/>
    my $aref = [<br/>
        [ &quot;fred&quot;, &quot;barney&quot;, &quot;pebbles&quot;, &quot;bambam&quot;, &quot;dino&quot;, ],<br/>
        [ &quot;homer&quot;, &quot;bart&quot;, &quot;marge&quot;, &quot;maggie&quot;, ],<br/>
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot;, &quot;judy&quot;, ],<br/>
    ];<br/>
<br/>
    print $aref[2][2];<br/>
<div class="spacer">
</div>
The compiler would immediately flag that as an error <i>at compile time</i>, because you were accidentally accessing @aref, an undeclared variable, and it would thereby remind you to write instead:<div class="spacer">
</div>
<br/>
    print $aref-&gt;[2][2]<br/>
</div>
<div class="section">
<h1>DEBUGGING</h1> You can use the debugger's &quot;x&quot; command to dump out complex data structures. For example, given the assignment to $AoA above, here's the debugger output:<div class="spacer">
</div>
<br/>
    DB&lt;1&gt; x $AoA<br/>
    $AoA = ARRAY(0x13b5a0)<br/>
       0  ARRAY(0x1f0a24)<br/>
          0  'fred'<br/>
          1  'barney'<br/>
          2  'pebbles'<br/>
          3  'bambam'<br/>
          4  'dino'<br/>
       1  ARRAY(0x13b558)<br/>
          0  'homer'<br/>
          1  'bart'<br/>
          2  'marge'<br/>
          3  'maggie'<br/>
       2  ARRAY(0x13b540)<br/>
          0  'george'<br/>
          1  'jane'<br/>
          2  'elroy'<br/>
          3  'judy'<br/>
</div>
<div class="section">
<h1>CODE EXAMPLES</h1> Presented with little comment (these will get their own manpages someday) here are short code examples illustrating access of various types of data structures.</div>
<div class="section">
<h1>ARRAYS OF ARRAYS</h1><div class="subsection">
<h2>Declaration of an ARRAY OF ARRAYS</h2><br/>
 @AoA = (<br/>
        [ &quot;fred&quot;, &quot;barney&quot; ],<br/>
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],<br/>
        [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],<br/>
      );<br/>
</div>
<div class="subsection">
<h2>Generation of an ARRAY OF ARRAYS</h2><br/>
 # reading from file<br/>
 while ( &lt;&gt; ) {<br/>
     push @AoA, [ split ];<br/>
 }<br/>
<br/>
 # calling a function<br/>
 for $i ( 1 .. 10 ) {<br/>
     $AoA[$i] = [ somefunc($i) ];<br/>
 }<br/>
<br/>
 # using temp vars<br/>
 for $i ( 1 .. 10 ) {<br/>
     @tmp = somefunc($i);<br/>
     $AoA[$i] = [ @tmp ];<br/>
 }<br/>
<br/>
 # add to an existing row<br/>
 push @{ $AoA[0] }, &quot;wilma&quot;, &quot;betty&quot;;<br/>
</div>
<div class="subsection">
<h2>Access and Printing of an ARRAY OF ARRAYS</h2><br/>
 # one element<br/>
 $AoA[0][0] = &quot;Fred&quot;;<br/>
<br/>
 # another element<br/>
 $AoA[1][1] =~ s/(\w)/\u$1/;<br/>
<br/>
 # print the whole thing with refs<br/>
 for $aref ( @AoA ) {<br/>
     print &quot;\t [ @$aref ],\n&quot;;<br/>
 }<br/>
<br/>
 # print the whole thing with indices<br/>
 for $i ( 0 .. $#AoA ) {<br/>
     print &quot;\t [ @{$AoA[$i]} ],\n&quot;;<br/>
 }<br/>
<br/>
 # print the whole thing one at a time<br/>
 for $i ( 0 .. $#AoA ) {<br/>
     for $j ( 0 .. $#{ $AoA[$i] } ) {<br/>
         print &quot;elt $i $j is $AoA[$i][$j]\n&quot;;<br/>
     }<br/>
 }<br/>
</div>
</div>
<div class="section">
<h1>HASHES OF ARRAYS</h1><div class="subsection">
<h2>Declaration of a HASH OF ARRAYS</h2><br/>
 %HoA = (<br/>
        flintstones        =&gt; [ &quot;fred&quot;, &quot;barney&quot; ],<br/>
        jetsons            =&gt; [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],<br/>
        simpsons           =&gt; [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],<br/>
      );<br/>
</div>
<div class="subsection">
<h2>Generation of a HASH OF ARRAYS</h2><br/>
 # reading from file<br/>
 # flintstones: fred barney wilma dino<br/>
 while ( &lt;&gt; ) {<br/>
     next unless s/^(.*?):\s*//;<br/>
     $HoA{$1} = [ split ];<br/>
 }<br/>
<br/>
 # reading from file; more temps<br/>
 # flintstones: fred barney wilma dino<br/>
 while ( $line = &lt;&gt; ) {<br/>
     ($who, $rest) = split /:\s*/, $line, 2;<br/>
     @fields = split ' ', $rest;<br/>
     $HoA{$who} = [ @fields ];<br/>
 }<br/>
<br/>
 # calling a function that returns a list<br/>
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {<br/>
     $HoA{$group} = [ get_family($group) ];<br/>
 }<br/>
<br/>
 # likewise, but using temps<br/>
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {<br/>
     @members = get_family($group);<br/>
     $HoA{$group} = [ @members ];<br/>
 }<br/>
<br/>
 # append new members to an existing family<br/>
 push @{ $HoA{&quot;flintstones&quot;} }, &quot;wilma&quot;, &quot;betty&quot;;<br/>
</div>
<div class="subsection">
<h2>Access and Printing of a HASH OF ARRAYS</h2><br/>
 # one element<br/>
 $HoA{flintstones}[0] = &quot;Fred&quot;;<br/>
<br/>
 # another element<br/>
 $HoA{simpsons}[1] =~ s/(\w)/\u$1/;<br/>
<br/>
 # print the whole thing<br/>
 foreach $family ( keys %HoA ) {<br/>
     print &quot;$family: @{ $HoA{$family} }\n&quot;<br/>
 }<br/>
<br/>
 # print the whole thing with indices<br/>
 foreach $family ( keys %HoA ) {<br/>
     print &quot;family: &quot;;<br/>
     foreach $i ( 0 .. $#{ $HoA{$family} } ) {<br/>
         print &quot; $i = $HoA{$family}[$i]&quot;;<br/>
     }<br/>
     print &quot;\n&quot;;<br/>
 }<br/>
<br/>
 # print the whole thing sorted by number of members<br/>
 foreach $family ( sort { @{$HoA{$b}} &lt;=&gt; @{$HoA{$a}} } keys %HoA ) {<br/>
     print &quot;$family: @{ $HoA{$family} }\n&quot;<br/>
 }<br/>
<br/>
 # print the whole thing sorted by number of members and name<br/>
 foreach $family ( sort {<br/>
                            @{$HoA{$b}} &lt;=&gt; @{$HoA{$a}}<br/>
                                        ||<br/>
                                    $a cmp $b<br/>
            } keys %HoA )<br/>
 {<br/>
     print &quot;$family: &quot;, join(&quot;, &quot;, sort @{ $HoA{$family} }), &quot;\n&quot;;<br/>
 }<br/>
</div>
</div>
<div class="section">
<h1>ARRAYS OF HASHES</h1><div class="subsection">
<h2>Declaration of an ARRAY OF HASHES</h2><br/>
 @AoH = (<br/>
        {<br/>
            Lead     =&gt; &quot;fred&quot;,<br/>
            Friend   =&gt; &quot;barney&quot;,<br/>
        },<br/>
        {<br/>
            Lead     =&gt; &quot;george&quot;,<br/>
            Wife     =&gt; &quot;jane&quot;,<br/>
            Son      =&gt; &quot;elroy&quot;,<br/>
        },<br/>
        {<br/>
            Lead     =&gt; &quot;homer&quot;,<br/>
            Wife     =&gt; &quot;marge&quot;,<br/>
            Son      =&gt; &quot;bart&quot;,<br/>
        }<br/>
  );<br/>
</div>
<div class="subsection">
<h2>Generation of an ARRAY OF HASHES</h2><br/>
 # reading from file<br/>
 # format: LEAD=fred FRIEND=barney<br/>
 while ( &lt;&gt; ) {<br/>
     $rec = {};<br/>
     for $field ( split ) {<br/>
         ($key, $value) = split /=/, $field;<br/>
         $rec-&gt;{$key} = $value;<br/>
     }<br/>
     push @AoH, $rec;<br/>
 }<br/>
<br/>
<br/>
 # reading from file<br/>
 # format: LEAD=fred FRIEND=barney<br/>
 # no temp<br/>
 while ( &lt;&gt; ) {<br/>
     push @AoH, { split /[\s+=]/ };<br/>
 }<br/>
<br/>
 # calling a function  that returns a key/value pair list, like<br/>
 # &quot;lead&quot;,&quot;fred&quot;,&quot;daughter&quot;,&quot;pebbles&quot;<br/>
 while ( %fields = getnextpairset() ) {<br/>
     push @AoH, { %fields };<br/>
 }<br/>
<br/>
 # likewise, but using no temp vars<br/>
 while (&lt;&gt;) {<br/>
     push @AoH, { parsepairs($_) };<br/>
 }<br/>
<br/>
 # add key/value to an element<br/>
 $AoH[0]{pet} = &quot;dino&quot;;<br/>
 $AoH[2]{pet} = &quot;santa's little helper&quot;;<br/>
</div>
<div class="subsection">
<h2>Access and Printing of an ARRAY OF HASHES</h2><br/>
 # one element<br/>
 $AoH[0]{lead} = &quot;fred&quot;;<br/>
<br/>
 # another element<br/>
 $AoH[1]{lead} =~ s/(\w)/\u$1/;<br/>
<br/>
 # print the whole thing with refs<br/>
 for $href ( @AoH ) {<br/>
     print &quot;{ &quot;;<br/>
     for $role ( keys %$href ) {<br/>
         print &quot;$role=$href-&gt;{$role} &quot;;<br/>
     }<br/>
     print &quot;}\n&quot;;<br/>
 }<br/>
<br/>
 # print the whole thing with indices<br/>
 for $i ( 0 .. $#AoH ) {<br/>
     print &quot;$i is { &quot;;<br/>
     for $role ( keys %{ $AoH[$i] } ) {<br/>
         print &quot;$role=$AoH[$i]{$role} &quot;;<br/>
     }<br/>
     print &quot;}\n&quot;;<br/>
 }<br/>
<br/>
 # print the whole thing one at a time<br/>
 for $i ( 0 .. $#AoH ) {<br/>
     for $role ( keys %{ $AoH[$i] } ) {<br/>
         print &quot;elt $i $role is $AoH[$i]{$role}\n&quot;;<br/>
     }<br/>
 }<br/>
</div>
</div>
<div class="section">
<h1>HASHES OF HASHES</h1><div class="subsection">
<h2>Declaration of a HASH OF HASHES</h2><br/>
 %HoH = (<br/>
        flintstones =&gt; {<br/>
                lead      =&gt; &quot;fred&quot;,<br/>
                pal       =&gt; &quot;barney&quot;,<br/>
        },<br/>
        jetsons     =&gt; {<br/>
                lead      =&gt; &quot;george&quot;,<br/>
                wife      =&gt; &quot;jane&quot;,<br/>
                &quot;his boy&quot; =&gt; &quot;elroy&quot;,<br/>
        },<br/>
        simpsons    =&gt; {<br/>
                lead      =&gt; &quot;homer&quot;,<br/>
                wife      =&gt; &quot;marge&quot;,<br/>
                kid       =&gt; &quot;bart&quot;,<br/>
        },<br/>
 );<br/>
</div>
<div class="subsection">
<h2>Generation of a HASH OF HASHES</h2><br/>
 # reading from file<br/>
 # flintstones: lead=fred pal=barney wife=wilma pet=dino<br/>
 while ( &lt;&gt; ) {<br/>
     next unless s/^(.*?):\s*//;<br/>
     $who = $1;<br/>
     for $field ( split ) {<br/>
         ($key, $value) = split /=/, $field;<br/>
         $HoH{$who}{$key} = $value;<br/>
     }<br/>
<br/>
<br/>
 # reading from file; more temps<br/>
 while ( &lt;&gt; ) {<br/>
     next unless s/^(.*?):\s*//;<br/>
     $who = $1;<br/>
     $rec = {};<br/>
     $HoH{$who} = $rec;<br/>
     for $field ( split ) {<br/>
         ($key, $value) = split /=/, $field;<br/>
         $rec-&gt;{$key} = $value;<br/>
     }<br/>
 }<br/>
<br/>
 # calling a function  that returns a key,value hash<br/>
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {<br/>
     $HoH{$group} = { get_family($group) };<br/>
 }<br/>
<br/>
 # likewise, but using temps<br/>
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {<br/>
     %members = get_family($group);<br/>
     $HoH{$group} = { %members };<br/>
 }<br/>
<br/>
 # append new members to an existing family<br/>
 %new_folks = (<br/>
     wife =&gt; &quot;wilma&quot;,<br/>
     pet  =&gt; &quot;dino&quot;,<br/>
 );<br/>
<br/>
 for $what (keys %new_folks) {<br/>
     $HoH{flintstones}{$what} = $new_folks{$what};<br/>
 }<br/>
</div>
<div class="subsection">
<h2>Access and Printing of a HASH OF HASHES</h2><br/>
 # one element<br/>
 $HoH{flintstones}{wife} = &quot;wilma&quot;;<br/>
<br/>
 # another element<br/>
 $HoH{simpsons}{lead} =~ s/(\w)/\u$1/;<br/>
<br/>
 # print the whole thing<br/>
 foreach $family ( keys %HoH ) {<br/>
     print &quot;$family: { &quot;;<br/>
     for $role ( keys %{ $HoH{$family} } ) {<br/>
         print &quot;$role=$HoH{$family}{$role} &quot;;<br/>
     }<br/>
     print &quot;}\n&quot;;<br/>
 }<br/>
<br/>
 # print the whole thing  somewhat sorted<br/>
 foreach $family ( sort keys %HoH ) {<br/>
     print &quot;$family: { &quot;;<br/>
     for $role ( sort keys %{ $HoH{$family} } ) {<br/>
         print &quot;$role=$HoH{$family}{$role} &quot;;<br/>
     }<br/>
     print &quot;}\n&quot;;<br/>
 }<br/>
<br/>
<br/>
 # print the whole thing sorted by number of members<br/>
 foreach $family ( sort { keys %{$HoH{$b}} &lt;=&gt; keys %{$HoH{$a}} } keys %HoH ) {<br/>
     print &quot;$family: { &quot;;<br/>
     for $role ( sort keys %{ $HoH{$family} } ) {<br/>
         print &quot;$role=$HoH{$family}{$role} &quot;;<br/>
     }<br/>
     print &quot;}\n&quot;;<br/>
 }<br/>
<br/>
 # establish a sort order (rank) for each role<br/>
 $i = 0;<br/>
 for ( qw(lead wife son daughter pal pet) ) { $rank{$_} = ++$i }<br/>
<br/>
 # now print the whole thing sorted by number of members<br/>
 foreach $family ( sort { keys %{ $HoH{$b} } &lt;=&gt; keys %{ $HoH{$a} } } keys %HoH ) {<br/>
     print &quot;$family: { &quot;;<br/>
     # and print these according to rank order<br/>
     for $role ( sort { $rank{$a} &lt;=&gt; $rank{$b} }  keys %{ $HoH{$family} } ) {<br/>
         print &quot;$role=$HoH{$family}{$role} &quot;;<br/>
     }<br/>
     print &quot;}\n&quot;;<br/>
 }<br/>
</div>
</div>
<div class="section">
<h1>MORE ELABORATE RECORDS</h1><div class="subsection">
<h2>Declaration of MORE ELABORATE RECORDS</h2> Here's a sample showing how to create and use a record whose fields are of many different sorts:<div class="spacer">
</div>
<br/>
     $rec = {<br/>
         TEXT      =&gt; $string,<br/>
         SEQUENCE  =&gt; [ @old_values ],<br/>
         LOOKUP    =&gt; { %some_table },<br/>
         THATCODE  =&gt; \&amp;some_function,<br/>
         THISCODE  =&gt; sub { $_[0] ** $_[1] },<br/>
         HANDLE    =&gt; \*STDOUT,<br/>
     };<br/>
<br/>
     print $rec-&gt;{TEXT};<br/>
<br/>
     print $rec-&gt;{SEQUENCE}[0];<br/>
     $last = pop @ { $rec-&gt;{SEQUENCE} };<br/>
<br/>
     print $rec-&gt;{LOOKUP}{&quot;key&quot;};<br/>
     ($first_k, $first_v) = each %{ $rec-&gt;{LOOKUP} };<br/>
<br/>
     $answer = $rec-&gt;{THATCODE}-&gt;($arg);<br/>
     $answer = $rec-&gt;{THISCODE}-&gt;($arg1, $arg2);<br/>
<br/>
     # careful of extra block braces on fh ref<br/>
     print { $rec-&gt;{HANDLE} } &quot;a string\n&quot;;<br/>
<br/>
     use FileHandle;<br/>
     $rec-&gt;{HANDLE}-&gt;autoflush(1);<br/>
     $rec-&gt;{HANDLE}-&gt;print(&quot; a string\n&quot;);<br/>
</div>
<div class="subsection">
<h2>Declaration of a HASH OF COMPLEX RECORDS</h2><br/>
     %TV = (<br/>
        flintstones =&gt; {<br/>
            series   =&gt; &quot;flintstones&quot;,<br/>
            nights   =&gt; [ qw(monday thursday friday) ],<br/>
            members  =&gt; [<br/>
                { name =&gt; &quot;fred&quot;,    role =&gt; &quot;lead&quot;, age  =&gt; 36, },<br/>
                { name =&gt; &quot;wilma&quot;,   role =&gt; &quot;wife&quot;, age  =&gt; 31, },<br/>
                { name =&gt; &quot;pebbles&quot;, role =&gt; &quot;kid&quot;,  age  =&gt;  4, },<br/>
            ],<br/>
        },<br/>
<br/>
        jetsons     =&gt; {<br/>
            series   =&gt; &quot;jetsons&quot;,<br/>
            nights   =&gt; [ qw(wednesday saturday) ],<br/>
            members  =&gt; [<br/>
                { name =&gt; &quot;george&quot;,  role =&gt; &quot;lead&quot;, age  =&gt; 41, },<br/>
                { name =&gt; &quot;jane&quot;,    role =&gt; &quot;wife&quot;, age  =&gt; 39, },<br/>
                { name =&gt; &quot;elroy&quot;,   role =&gt; &quot;kid&quot;,  age  =&gt;  9, },<br/>
            ],<br/>
         },<br/>
<br/>
        simpsons    =&gt; {<br/>
            series   =&gt; &quot;simpsons&quot;,<br/>
            nights   =&gt; [ qw(monday) ],<br/>
            members  =&gt; [<br/>
                { name =&gt; &quot;homer&quot;, role =&gt; &quot;lead&quot;, age  =&gt; 34, },<br/>
                { name =&gt; &quot;marge&quot;, role =&gt; &quot;wife&quot;, age =&gt; 37, },<br/>
                { name =&gt; &quot;bart&quot;,  role =&gt; &quot;kid&quot;,  age  =&gt;  11, },<br/>
            ],<br/>
         },<br/>
      );<br/>
</div>
<div class="subsection">
<h2>Generation of a HASH OF COMPLEX RECORDS</h2><br/>
     # reading from file<br/>
     # this is most easily done by having the file itself be<br/>
     # in the raw data format as shown above.  perl is happy<br/>
     # to parse complex data structures if declared as data, so<br/>
     # sometimes it's easiest to do that<br/>
<br/>
     # here's a piece by piece build up<br/>
     $rec = {};<br/>
     $rec-&gt;{series} = &quot;flintstones&quot;;<br/>
     $rec-&gt;{nights} = [ find_days() ];<br/>
<br/>
     @members = ();<br/>
     # assume this file in field=value syntax<br/>
     while (&lt;&gt;) {<br/>
         %fields = split /[\s=]+/;<br/>
         push @members, { %fields };<br/>
     }<br/>
     $rec-&gt;{members} = [ @members ];<br/>
<br/>
     # now remember the whole thing<br/>
     $TV{ $rec-&gt;{series} } = $rec;<br/>
<br/>
     ###########################################################<br/>
     # now, you might want to make interesting extra fields that<br/>
     # include pointers back into the same data structure so if<br/>
     # change one piece, it changes everywhere, like for example<br/>
     # if you wanted a {kids} field that was a reference<br/>
     # to an array of the kids' records without having duplicate<br/>
     # records and thus update problems.<br/>
     ###########################################################<br/>
     foreach $family (keys %TV) {<br/>
         $rec = $TV{$family}; # temp pointer<br/>
         @kids = ();<br/>
         for $person ( @{ $rec-&gt;{members} } ) {<br/>
             if ($person-&gt;{role} =~ /kid|son|daughter/) {<br/>
                 push @kids, $person;<br/>
             }<br/>
         }<br/>
         # REMEMBER: $rec and $TV{$family} point to same data!!<br/>
         $rec-&gt;{kids} = [ @kids ];<br/>
     }<br/>
<br/>
     # you copied the array, but the array itself contains pointers<br/>
     # to uncopied objects. this means that if you make bart get<br/>
     # older via<br/>
<br/>
     $TV{simpsons}{kids}[0]{age}++;<br/>
<br/>
     # then this would also change in<br/>
     print $TV{simpsons}{members}[2]{age};<br/>
<br/>
     # because $TV{simpsons}{kids}[0] and $TV{simpsons}{members}[2]<br/>
     # both point to the same underlying anonymous hash table<br/>
<br/>
     # print the whole thing<br/>
     foreach $family ( keys %TV ) {<br/>
         print &quot;the $family&quot;;<br/>
         print &quot; is on during @{ $TV{$family}{nights} }\n&quot;;<br/>
         print &quot;its members are:\n&quot;;<br/>
         for $who ( @{ $TV{$family}{members} } ) {<br/>
             print &quot; $who-&gt;{name} ($who-&gt;{role}), age $who-&gt;{age}\n&quot;;<br/>
         }<br/>
         print &quot;it turns out that $TV{$family}{lead} has &quot;;<br/>
         print scalar ( @{ $TV{$family}{kids} } ), &quot; kids named &quot;;<br/>
         print join (&quot;, &quot;, map { $_-&gt;{name} } @{ $TV{$family}{kids} } );<br/>
         print &quot;\n&quot;;<br/>
     }<br/>
</div>
</div>
<div class="section">
<h1>Database Ties</h1> You cannot easily tie a multilevel data structure (such as a hash of hashes) to a dbm file.  The first problem is that all but GDBM and Berkeley DB have size limitations, but beyond that, you also have problems with how references are to be represented on disk.  One experimental module that does partially attempt to address this need is the MLDBM module.  Check your nearest CPAN site as described in perlmodlib for source code to MLDBM.</div>
<div class="section">
<h1>SEE ALSO</h1> perlref, perllol, perldata, perlobj</div>
<div class="section">
<h1>AUTHOR</h1> Tom Christiansen &lt; <i>tchrist@perl.com</i>&gt;</div>
<table class="foot">
<tr>
<td class="foot-date">
2014-03-24</td>
<td class="foot-os">
perl v5.20.2</td>
</tr>
</table>
</div>
</body>
</html>

